plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.palantir.git-version" version "0.12.3"
}

archivesBaseName = 'Bingo'

sourceCompatibility = JavaVersion.VERSION_11

compileJava.options.compilerArgs = ['-Xlint:unchecked','-Xlint:deprecation']

group 'com.jtprince.bingo'
version gitVersion()

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = "https://raw.githubusercontent.com/JorelAli/CommandAPI/mvn-repo/" }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    shadow 'org.jetbrains:annotations:20.1.0'
    shadow 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.java-websocket:Java-WebSocket:1.5.1'
    implementation 'dev.jorel:commandapi-shade:5.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("plugin.yml") {
      expand(project.properties)
    }
}

shadowJar {
    archiveBaseName.set("Bingo")
    archiveClassifier.set("")
    archiveVersion.set("")
}

task(listAutoGoals, type: JavaExec) {
    main = 'com.jtprince.bingo.plugin.automarking.AutomatedGoalList'
    classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
}

task(dumpAutoGoals, type: JavaExec) {
    main = 'com.jtprince.bingo.plugin.automarking.AutomatedGoalList'
    classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
    args 'automated_goals.txt'
}

project.tasks.assemble.dependsOn shadowJar
project.tasks.assemble.dependsOn dumpAutoGoals
